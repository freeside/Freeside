<%doc>

#Example:

  include( 'elements/svc_Common.html, 

             'table' => 'svc_something'

             'labels' => {
                           'column' => 'Label',
                         },

             #listref - each item is a literal column name (or method) or
             # (notyet) coderef.  if not specified all columns (except for the
             #primary key) will be viewable
             'fields' => [
                         ]

             # defaults to "edit/$table.cgi?", will have svcnum appended
             'edit_url' => 

             #at the very bottom (well, as low as you can go from here)
             'html_foot'  => '',

         )

</%doc>
<SCRIPT>
function areyousure(href) {
% my $delmsg = emt("Permanently delete this [_1]?", $label);
  if (confirm(<% $delmsg |js_string %>) == true)
    window.location.href = href;
}
</SCRIPT>

% if ( $custnum ) { 

  <& /elements/header.html, mt("View [_1]: [_2]",$label,$value) &>

  <& /elements/small_custview.html, $custnum, '', 1,
     "${p}view/cust_main.cgi" &>
  <BR>

% } else { 

  <& /elements/header.html, mt("View [_1]: [_2]",$label,$value), menubar(
      emt("Cancel this (unaudited) [_1]",$label) =>
            "javascript:areyousure(\'${p}misc/cancel-unaudited.cgi?$svcnum\')"
  ) &>

% } 

% if ( $opt{radius_usage} ) {
    <& svc_radius_usage.html,
              'svc'      => $svc_x,
              'part_svc' => $part_svc,
              'cust_pkg' => $cust_pkg,
    &>
% }

<% mt('Service #') |h %><B><% $svcnum %></B>
% if ( $custnum ) {
%   my $url = $opt{'edit_url'} || $p. 'edit/'. $opt{'table'}. '.cgi?';
<& /view/elements/svc_edit_link.html, 'svc' => $svc_x, 'edit_url' => $url &>
% }
<BR>

<% ntable("#cccccc") %><TR><TD><% ntable("#cccccc",2) %>

% my @inventory_items = $svc_x->inventory_item;
% foreach my $f ( @$fields ) {
%
%   my($field, $type, $value);
%   if ( ref($f) ) {
%     $field = $f->{'field'};
%     $type  = $f->{'type'} || 'text';
%     if ( $f->{'value_callback'} ) {
%       my $hack_strict_refs = \&{ $f->{'value_callback'} };
%       $value = &$hack_strict_refs($svc_x);
%     } else {
%       $value = encode_entities($svc_x->$field);
%     }
%   } else {
%     $field = $f;
%     $type = 'text';
%     $value = encode_entities($svc_x->$field);
%   }
%
%   my $columndef = $part_svc->part_svc_column($field);
%   if ( $columndef->columnflag =~ /^[MA]$/ && $columndef->columnvalue =~ /,/ )
%   {
%     # inventory-select field with multiple classes
%     # show the class name to disambiguate
%     my ($item) = grep { $_->svc_field eq $field } @inventory_items;
%     my $class = qsearchs('inventory_class', { classnum => $item->classnum });
%     $value .= ' <i>('. $class->classname . ')</i>' if $class;
%   }
%   unless ($columndef->columnflag eq 'F' && !length($columndef->columnvalue)) {

      <TR>
        <TD ALIGN="right">
          <% ( $opt{labels} && exists $opt{labels}->{$field} )
                  ? $opt{labels}->{$field}
                  : $field
          %>
        </TD>

%	$value = time2str($date_format,$value)
%         if $type eq 'date' && $value;
%	$value = time2str("$date_format %H:%M",$value)
%         if $type eq 'datetime' && $value;
%	$value = $value eq 'Y' ? emt('Yes') : emt('No')
%         if $type eq 'checkbox';
%       $value .= ' ('. (Net::MAC::Vendor::lookup($value))->[0]. ')'
%         if $type =~ /mac_addr$/ && $value =~ /\w/i;
%       #eventually more options for <SELECT>, etc. fields

        <TD BGCOLOR="#ffffff"><% $value %><TD>

      </TR>

%   }
%
% } 

% foreach (sort { $a cmp $b } $svc_x->virtual_fields) { 
  <% $svc_x->pvf($_)->widget('HTML', 'view', $svc_x->getfield($_)) %>
% } 


</TABLE></TD></TR></TABLE>

<BR>

<& svc_devices.html,
     'svc_x' => $svc_x,
     'table' => $svc_x->device_table,
&>

% if ( defined($opt{'html_foot'}) ) {

  <% ref($opt{'html_foot'})
       ? &{ $opt{'html_foot'} }($svc_x)
       : $opt{'html_foot'}
  %>
  <BR>

% }

% if ( $cust_svc ) {
<& /elements/table-tickets.html, object => $cust_svc &>
% }

<% joblisting({'svcnum'=>$svcnum}, 1) %>

<% include('/elements/footer.html') %>
<%init>

die "access denied"
  unless $FS::CurrentUser::CurrentUser->access_right('View customer services');

my(%opt) = @_;

my $conf = new FS::Conf;
my $date_format = $conf->config('date_format') || '%m/%d/%Y';

my $table = $opt{'table'};

my $fields = $opt{'fields'}
             #|| [ grep { $_ ne 'svcnum' } dbdef->table($table)->columns ];
             || [ grep { $_ ne 'svcnum' } fields($table) ];

my $svcnum;
if ( $cgi->param('svcnum') ) {
  $cgi->param('svcnum') =~ /^(\d+)$/ or die "unparseable svcnum";
  $svcnum = $1;
} else {
  my($query) = $cgi->keywords;
  $query =~ /^(\d+)$/ or die "no svcnum";
  $svcnum = $1;
}
my $svc_x = qsearchs({
  'select'    => $opt{'table'}.'.*',
  'table'     => $opt{'table'},
  'addl_from' => ' LEFT JOIN cust_svc  USING ( svcnum  ) '.
                 ' LEFT JOIN cust_pkg  USING ( pkgnum  ) '.
                 ' LEFT JOIN cust_main USING ( custnum ) ',
  'hashref'   => { 'svcnum' => $svcnum },
  'extra_sql' => ' AND '. $FS::CurrentUser::CurrentUser->agentnums_sql(
                            'null_right' => 'View/link unlinked services'
                          ),
}) or die "Unknown svcnum $svcnum in ". $opt{'table'}. " table\n";

my $cust_svc = $svc_x->cust_svc;
my ($label, $value, $svcdb, $part_svc );
my $labels = $opt{labels}; #not -> here

if ( $cust_svc ) {
  ($label, $value, $svcdb) = $cust_svc->label;

  $part_svc = $cust_svc->part_svc;

  #false laziness w/edit/svc_Common.html
  #override default labels with service-definition labels if applicable
  foreach my $field ( keys %$labels ) {
    my $col = $part_svc->part_svc_column($field);
    $labels->{$field} = $col->columnlabel if $col->columnlabel !~ /^\s*$/;
  }
} else {
  $label = "Unlinked $table";
  $value = $svc_x->label;
  $svcdb = $table;
  # just to satisfy callbacks
  $part_svc = FS::part_svc->new({ svcpart => 0, svcdb => $table });
}

my $pkgnum = $cust_svc->pkgnum if $cust_svc;

my($cust_pkg, $custnum);
if ($pkgnum) {
  $cust_pkg = $cust_svc->cust_pkg;
  $custnum = $cust_pkg->custnum;
} else {
  $cust_pkg = '';
  $custnum = '';
}

# attached routers
if ( my $router = qsearchs('router', { svcnum => $svc_x->svcnum }) ) {
  push @$fields, qw(router_routername router_block);
  $labels->{'router_routername'} = 'Attached router';
  $labels->{'router_block'} = 'Attached address block';
  $svc_x->set('router_routername', $router->routername);
  my $block = qsearchs('addr_block', { routernum => $router->routernum });
  if ( $block ) {
    $svc_x->set('router_block', $block->cidr);
  } else {
    $svc_x->set('router_block', '<i>(none)</i>');
  }
}

&{ $opt{'svc_callback'} }( $cgi, $svc_x, $part_svc, $cust_pkg, $fields, \%opt ) 
    if $opt{'svc_callback'};
</%init>
