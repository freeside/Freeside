% if ( $error ) {
%   $cgi->param('error', $error);
<% $cgi->redirect(popurl(1). "pref.html?". $cgi->query_string ) %>
% } else {
<% include('/elements/header.html', 'Preferences updated') %>
<% include('/elements/footer.html') %>
% }
<%init>

if ( FS::Conf->new->exists('disable_acl_changes') ) {
  errorpage("Preference changes disabled in public demo");
  die "shouldn't be reached";
}

my $error = '';

if ( FS::Auth->auth_class->can('change_password')
       && grep { $cgi->param($_) !~ /^\s*$/ }
            qw(_password new_password new_password2)
   ) {

  if ( $cgi->param('new_password') ne $cgi->param('new_password2') ) {
    $error = "New passwords don't match";

  } elsif ( ! length($cgi->param('new_password')) ) {
    $error = 'No new password entered';

  } elsif ( ! FS::Auth->authenticate( $FS::CurrentUser::CurrentUser,
                                      scalar($cgi->param('_password')) )
          ) {
    $error = 'Current password incorrect; password not changed';

  } else {

    $error = $FS::CurrentUser::CurrentUser->change_password(
      scalar($cgi->param('new_password'))
    );

  }

}

my $access_user = $FS::CurrentUser::CurrentUser;

#well, if you got your password change wrong, you don't get anything else
#changed right now.  but it should be sticky on the form
unless ( $error ) { # if ($access_user) {

  my %param = $access_user->options;

  #XXX autogen
  my @paramlist = qw( locale menu_position default_customer_view 
                      history_order
                      spreadsheet_format mobile_menu
                      enable_fuzzy_on_exact
                      disable_html_editor disable_enter_submit_onetimecharge
                      enable_mask_clipboard_hack dashboard_customers
                      email_address
                      snom-ip snom-username snom-password
                      vonage-fromnumber vonage-username vonage-password
                      cust_pkg-display_times
                      show_pkgnum show_confitem_counts export_getsettings
                      show_db_profile save_db_profile save_tmp_typesetting
                      height width availHeight availWidth colorDepth
                    );

  foreach (@paramlist) {
    scalar($cgi->param($_)) =~ /^[,.\-\@\w]*$/ && next;
    $error ||= "Illegal value for parameter $_";
    last;
  }

  foreach (@paramlist) {
    $param{$_} = scalar($cgi->param($_));
  }

  $error ||= $access_user->replace( \%param );

}

if ( !$error and ($FS::TicketSystem::system || '') eq 'RT_Internal' ) {
  # sync RT user locale on every update
  my $locale = $access_user->option('locale');
  FS::TicketSystem->init;
  my $UserObj = FS::TicketSystem->session('')->{'CurrentUser'}->UserObj;
  # Bypass RT ModifySelf ACL
  $UserObj->CurrentUser( $RT::SystemUser );
  if ( $UserObj->Lang ne $locale ) {
    my ($val, $msg) = $UserObj->SetLang($locale);
    $error = $msg if !$val;
  }
}
</%init>
