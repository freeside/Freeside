<%doc>

Example:

  include( '/elements/select-table.html',

    ##
    # required
    ##
    'table'          => 'table_name',
    'name_col'       => 'name_column',
   
    #strongly recommended (you want your forms to be "sticky" on errors, right?)
    'curr_value'     => 'current_value',
    #'value' => #deprecated form of 'curr_value',
   
    ##
    # optional
    ##

    #search params
    'hashref'          => {},
    'addl_from'        => '',
    'extra_sql'        => '',
    'agent_virt'       => 0, #set true and make sure the result is JOINed to
                             #something with agentnum (usually cust_main)
    'agent_null'       => 0, #set true to always show un-agented entries
    'agent_null_right' => '', #right to see un-agented entries
    #or
    'records'        => \@records, #instead of search params

    #basic params controlling the resulting <SELECT>
    'pre_options'    => [ 'value' => 'option' ], #before normal options
    'post_options'   => [ 'value' => 'option' ], #after normal options
    'empty_label'    => '', #better specify it though, the default might change
    'multiple'       => 0, # bool
    'disable_empty'  => 0, # bool (implied by multiple)
    'label_showkey'  => 0, # bool
    'label_callback' => sub { my $record = shift; return "label"; },

    #more params controlling HTML stuff about the <SELECT>
    'element_name'   => '', #HTML element name, defaults to the name of
                            # the primary key column
    'field'          => '', #synonym for element_name
    'element_etc'    => '', #additional attributes (i.e. "DISABLED") for the
                            #<SELECT> element
    'onchange'       => '', #javascript code

    #params (well, a param) controlling the <OPTION>s
    'extra_option_attributes' => [ 'field' ], #field or method in $table objects
                                              #(are prefixed w/data- per HTML5)

    #special return options
    'js_only'      => 0, #set true to return only the JS portions (i.e. nothing)
    'html_only'    => 0, #set true to return only the HTML portions (no-op, i.e. return everything)

    #debugging
    'debug'          => 0, #set true to enable

  )

</%doc>
% unless ( $opt{'js_only'} ) {

<SELECT <% $opt{'multiple'} ? 'MULTIPLE' : '' %>
        NAME = "<% $opt{'element_name'} || $opt{'field'} || $key %>"
        ID   = "<% $opt{'id'} || $key %>"
        <% $onchange %>
        <% $opt{'element_etc'} %>
>

% while ( @pre_options ) { 
%   my $pre_opt   = shift(@pre_options);
%   my $pre_label = shift(@pre_options);
%   my $selected =    ( ref($value) && $value->{$pre_opt} )
%                  || ( $value eq $pre_opt );
    <OPTION VALUE="<% $pre_opt %>"
            <% $selected ? 'SELECTED' : '' %>
    ><% $pre_label %>
% } 

% unless ( $opt{'multiple'} || $opt{'disable_empty'} ) {
    <OPTION VALUE=""><% $opt{'empty_label'} || 'all' %>
% }

% foreach my $record ( sort {    $a->$name_col() cmp $b->$name_col()
%                             || $a->$key()      <=> $b->$key()
%                           }
%                           @records
%                    )
% {
%   my $recvalue = $record->$key();
    <OPTION VALUE="<% $recvalue %>"
            <% $opt{'all_selected'} || ref($value) && $value->{$recvalue} || $value == $recvalue
               ? ' SELECTED' : ''
            %>
%           foreach my $att ( @{ $opt{'extra_option_attributes'} } ) {
              data-<% $att %>="<% $record->$att() |h %>"
%           }
    ><% $opt{'label_showkey'} ? "$recvalue: " : '' %>
     <% $opt{'label_callback'}
          ? &{ $opt{'label_callback'} }( $record )
          : $record->$name_col()
     %>
% } 

% while ( @post_options ) { 
%   my $post_opt   = shift(@post_options);
%   my $post_label = shift(@post_options);
%   my $selected =    ( ref($value) && $value->{$post_opt} )
%                  || ( $value eq $post_opt );
    <OPTION VALUE="<% $post_opt %>"
            <% $selected ? 'SELECTED' : '' %>
    ><% $post_label %>
% } 

</SELECT>

%}
<%init>

my( %opt ) = @_;

warn "elements/select-table.html: \n". Dumper(%opt)
  if exists $opt{debug} && $opt{debug};

$opt{'extra_option_attributes'} ||= [];

my $onchange = '';
if ( $opt{'onchange'} ) {
  $onchange = $opt{'onchange'};
  $onchange .= '(this)' unless $onchange =~ /\(\w*\);?$/;
  $onchange =~ s/\(what\);/\(this\);/g; #ugh, terrible hack.  all onchange
                                        #callbacks should act the same
  $onchange = 'onChange="'. $onchange. '"';
}

my $dbdef_table = dbdef->table($opt{'table'})
  or die "can't find dbdef for ". $opt{'table'}. " table\n";

my $key = $dbdef_table->primary_key; #? $opt{'primary_key'} ||

my $name_col = $opt{'name_col'};

my $value = $opt{'curr_value'} || $opt{'value'};
$value = [ split(/\s*,\s*/, $value) ] if $opt{'multiple'} && $value =~ /,/;

#my $addl_from = $opt{'addl_from'} || '';
my $extra_sql = $opt{'extra_sql'} || '';
my $hashref   = $opt{'hashref'} || {};

if ( $opt{'agent_virt'} ) {
  $extra_sql .=
    ( $extra_sql =~ /WHERE/i || scalar(keys %$hashref ) ? ' AND ' : ' WHERE ' ).
    $FS::CurrentUser::CurrentUser->agentnums_sql(
                                    'null'       => $opt{'agent_null'},
                                    'null_right' => $opt{'agent_null_right'},
                                   );
}

my @records = ();
if ( $opt{'records'} ) {
  @records = @{ $opt{'records'} };
} else {
  @records = qsearch( {
    'table'     => $opt{'table'},
    'addl_from' => $opt{'addl_from'},
    'hashref'   => $hashref,
    'extra_sql' => $extra_sql,
    'order_by'  => ( $opt{'order_by'} || "ORDER BY $name_col" ),
  });
}

unless (    $value < 1 # !$value #ignore negatives too
         or ref($value)
         or ! exists( $opt{hashref}->{disabled} ) #??
         or grep { $value == $_->$key() } @records
       ) {
  delete $opt{hashref}->{disabled};
  $opt{hashref}->{$key} = $value;
  my $record = qsearchs( {
    'table'     => $opt{table},
    'addl_from' => $opt{'addl_from'},
    'hashref'   => $hashref,
    'extra_sql' => $extra_sql,
  });
  push @records, $record if $record;
}

if ( ref( $value ) eq 'ARRAY' ) {
  $value = { map { $_ => 1 } @$value };
}

my @pre_options  = $opt{pre_options}  ? @{ $opt{pre_options} } : ();
my @post_options = $opt{post_options} ? @{ $opt{post_options} } : ();

</%init>
