<& elements/search.html,
                  'title'       => emt('Customer Search Results'), 
                  'menubar'     => $menubar,
                  'name'        => emt('customers'),
                  'query'       => $sql_query,
                  'count_query' => $count_query,
                  'header'      => [ FS::UI::Web::cust_header(
                                       $cgi->param('cust_fields')
                                     ),
                                     @extra_headers,
                                   ],
                  'fields'      => [
                    \&FS::UI::Web::cust_fields,
                    @extra_fields,
                  ],
                  'color'       => [ FS::UI::Web::cust_colors(),
                                     map '', @extra_fields
                                   ],
                  'style'       => [ FS::UI::Web::cust_styles(),
                                     map '', @extra_fields
                                   ],
                  'align'       => [ FS::UI::Web::cust_aligns(),
                                     map '', @extra_fields
                                   ],
                  'links'       => [ ( map { $_ ne 'Cust. Status' ? $link : '' }
                                           FS::UI::Web::cust_header(
                                                      $cgi->param('cust_fields')
                                                                   )
                                     ),
                                     map '', @extra_fields
                                   ],
&>
<%init>

die "access denied"
  unless $FS::CurrentUser::CurrentUser->access_right('Advanced customer search');

my %search_hash = ();

#$search_hash{'query'} = $cgi->keywords;

#scalars
my @scalars = qw (
  agentnum salesnum status address zip paydate_year paydate_month invoice_terms
  no_censustract with_geocode with_email POST no_POST
  custbatch usernum
  cancelled_pkgs
  cust_fields flattened_pkgs
  all_tags
  all_pkg_classnums
  any_pkg_status
);

for my $param ( @scalars ) {
  $search_hash{$param} = scalar( $cgi->param($param) )
    if length($cgi->param($param));
}

#lists
for my $param (qw( classnum refnum payby tagnum pkg_classnum )) {
  $search_hash{$param} = [ $cgi->param($param) ];
}

###
# parse dates
###

foreach my $field (qw( signupdate birthdate spouse_birthdate anniversary_date )) {

  my($beginning, $ending) = FS::UI::Web::parse_beginning_ending($cgi, $field);

  next if $beginning == 0 && $ending == 4294967295 && ( $field ne 'signupdate' || !defined($cgi->param('signuphour')) );
       #or $disable{$cgi->param('status')}->{$field};

  unless ( $field eq 'signupdate' ) {
    $beginning -= 43200;
    $ending    -= 43200;
  }

  my @ary = ( $beginning, $ending );
  push @ary, scalar($cgi->param('signuphour')) if $field eq 'signupdate';

  $search_hash{$field} = \@ary;

}

##
# amounts
##

$search_hash{'current_balance'} =
  [ FS::UI::Web::parse_lt_gt($cgi, 'current_balance') ];

###
# etc
###

my $sql_query = FS::cust_main::Search->search(\%search_hash);
my $count_query   = delete($sql_query->{'count_query'});
my @extra_headers = @{ delete($sql_query->{'extra_headers'}) };
my @extra_fields  = @{ delete($sql_query->{'extra_fields'})  };

my $link = [ "${p}view/cust_main.cgi?", 'custnum' ];

###
# email links
###

my $menubar = [];

if ( $FS::CurrentUser::CurrentUser->access_right('Bulk send customer notices') ) {

  my $uri = new URI;
  $uri->query_form( \%search_hash );
  my $query = $uri->query;

  push @$menubar, emt('Email a notice to these customers') =>
                    "${p}misc/email-customers.html?table=cust_main&$query",

}

</%init>
