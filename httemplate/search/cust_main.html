<& elements/search.html,
                  'title'       => emt('Customer Search Results'), 
                  'menubar'     => $menubar,
                  'name'        => emt('customers'),
                  'query'       => $sql_query,
                  'count_query' => $count_query,
                  'header'      => [ FS::UI::Web::cust_header(
                                       $cgi->param('cust_fields')
                                     ),
                                     @extra_headers,
                                   ],
                  'fields'      => [
                    \&FS::UI::Web::cust_fields,
                    @extra_fields,
                  ],
                  'sort_fields' => [
                    FS::UI::Web::cust_sort_fields(),
                    @extra_sort_fields,
                  ],
                  'color'       => [ FS::UI::Web::cust_colors(),
                                     map '', @extra_fields
                                   ],
                  'style'       => [ FS::UI::Web::cust_styles(),
                                     map '', @extra_fields
                                   ],
                  'align'       => [ FS::UI::Web::cust_aligns(),
                                     map '', @extra_fields
                                   ],
                  'links'       => [ ( map { $_ ne 'Cust. Status' ? $link : '' }
                                           FS::UI::Web::cust_header(
                                                      $cgi->param('cust_fields')
                                                                   )
                                     ),
                                     map  { $_ eq 'num_referrals'
                                              ? $referral_link
                                              : ''
                                          }
                                       @extra_fields
                                   ],
&>
<%init>

die "access denied"
  unless $FS::CurrentUser::CurrentUser->access_right('Advanced customer search');

my %search_hash = ();

#$search_hash{'query'} = $cgi->keywords;

#scalars
my @scalars = qw (
  agentnum salesnum status
  address city county state zip country location_history
  daytime night mobile fax
  invoice_terms
  no_censustract with_geocode with_email tax no_tax POST no_POST
  custbatch usernum
  cancelled_pkgs
  cust_fields flattened_pkgs
  all_tags
  all_pkg_classnums
  any_pkg_status
  with_referrals referral_status
);

for my $param ( @scalars ) {
  $search_hash{$param} = scalar( $cgi->param($param) )
    if length($cgi->param($param));
}

#lists
for my $param (qw( classnum refnum pkg_classnum )) {
  $search_hash{$param} = [ $cgi->multi_param($param) ];
}

my $params = $cgi->Vars;

#contacts
$search_hash{'contacts'} = {
  map { $_ => $cgi->param($_), }
    grep { /^(contacts_*)/ && $cgi->param($_) }
      keys %$params
};

#tags
$search_hash{'tagnum'} = [
  map { /^tagnum(\d+)/ && $1 }
    grep { /^tagnum(\d+)/ && $cgi->param($_) }
      keys %$params
];

###
# parse dates
###

foreach my $field (qw( signupdate birthdate spouse_birthdate anniversary_date )) {

  my($beginning, $ending) = FS::UI::Web::parse_beginning_ending($cgi, $field);

  next if $beginning == 0 && $ending == 4294967295 && ( $field ne 'signupdate' || !defined($cgi->param('signuphour')) );
       #or $disable{$cgi->param('status')}->{$field};

  unless ( $field eq 'signupdate' ) {
    $beginning -= 43200;
    $ending    -= 43200;
  }

  my @ary = ( $beginning, $ending );
  push @ary, scalar($cgi->param('signuphour')) if $field eq 'signupdate';

  $search_hash{$field} = \@ary;

}

##
# amounts
##

$search_hash{'current_balance'} =
  [ FS::UI::Web::parse_lt_gt($cgi, 'current_balance') ];

###
# etc
###

my $sql_query = FS::cust_main::Search->search(\%search_hash);
my $count_query = delete($sql_query->{'count_query'});
my @extra_headers     = @{ delete($sql_query->{'extra_headers'}) };
my @extra_fields      = @{ delete($sql_query->{'extra_fields'})  };
my @extra_sort_fields = @{ delete($sql_query->{'extra_sort_fields'})  };

my $link = [ "${p}view/cust_main.cgi?", 'custnum' ];

my $referral_link = [ "${p}search/cust_main.cgi?referral_custnum=", 'custnum' ];

###
# email links
###

my $menubar = [];

if ( $FS::CurrentUser::CurrentUser->access_right('Bulk send customer notices') ) {

  # set so invoice emails are default if showing that field.
  $search_hash{'classnums'} = 'invoice' if $cgi->param('cust_fields') =~ /Invoicing email/;

  # URI::query_from does not support hashref
  #   results in: ...&contacts=HASH(0x55e16cb81da8)&...
  my %query_hash = %search_hash;
  foreach (keys %{$query_hash{contacts}}) { $query_hash{$_} = $query_hash{contacts}{$_}; }

  delete $query_hash{contacts}
    if exists $query_hash{contacts} && ref $query_hash{contacts};

  my $uri = new URI;
  $uri->query_form( \%query_hash );
  my $query = $uri->query;

  push @$menubar, emt('Email a notice to these customers message recipients') =>
                    "${p}misc/email-customers.html?table=cust_main&classnums=message&$query";
  push @$menubar, emt('Email a notice to these customers invoice recipients') =>
                    "${p}misc/email-customers.html?table=cust_main&classnums=invoice&$query";
}

###
# agent transfer link
###

my $agent_transfer_link = include('/elements/agent_transfer_link.html', {
     'search' => \%search_hash,
});
push @$menubar, emt('popup') => '<div style="display:inline-block">'.$agent_transfer_link.'</div>';

</%init>
