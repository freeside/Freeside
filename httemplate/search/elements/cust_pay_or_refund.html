<%doc>

Examples:

  include( 'elements/cust_pay_or_refund.html',
               'thing'          => 'pay',
               'amount_field'   => 'paid',
               'name_singular'  => 'payment',
               'name_verb'      => 'paid',
         )

  include( 'elements/cust_pay_or_refund.html',
               'thing'          => 'refund',
               'amount_field'   => 'refund',
               'name_singular'  => 'refund',
               'name_verb'      => 'refunded',
         )

  include( 'elements/cust_pay_or_refund.html',
               'thing'          => 'pay_pending',
               'amount_field'   => 'paid',
               'name_singular'  => 'pending payment',
               'name_verb'      => 'pending',
               'disable_link'   => 1,
               'disable_by'     => 1,
               'html_init'      => '',
               'addl_header'    => [],
               'addl_fields'    => [],
               'redirect_empty' => $redirect_empty,
          )

</%doc>
<% include( 'search.html',
                'title'          => $title,
                'name_singular'  => $name_singular,
                'query'          => $sql_query,
                'count_query'    => $count_query,
                'count_addl'     => [ '$%.2f total '.$opt{name_verb}, ],
                'redirect_empty' => $opt{'redirect_empty'},
                'header'         => [ "\u$name_singular",
                                      'Amount',
                                      'Date',
                                      @header,
                                      FS::UI::Web::cust_header(),
                                    ],
                'fields'       => [
                  'payby_payinfo_pretty',
                  sub { sprintf('$%.2f', shift->$amount_field() ) },
                  sub { time2str('%b %d %Y', shift->_date ) },
                  @fields,
                  \&FS::UI::Web::cust_fields,
                ],
                #'align' => 'lrrrll',
                'align' => 'rrr'.
                           join('', map 'c', @fields ).
                           FS::UI::Web::cust_aligns(),
                'links' => [
                  $link,
                  $link,
                  $link,
                  ( map '', @fields ),
                  ( map { $_ ne 'Cust. Status' ? $cust_link : '' }
                        FS::UI::Web::cust_header()
                  ),
                ],
                'color' => [ 
                             '',
                             '',
                             '',
                             ( map '', @fields ),
                             FS::UI::Web::cust_colors(),
                           ],
                'style' => [ 
                             '',
                             '',
                             '',
                             ( map '', @fields ),
                             FS::UI::Web::cust_styles(),
                           ],
          )
%>
<%init>

my %opt = @_;

my $curuser = $FS::CurrentUser::CurrentUser;

die "access denied"
  unless $curuser->access_right('Financial reports');

my $thing = $opt{'thing'};
my $amount_field = $opt{'amount_field'};
my $name_singular = $opt{'name_singular'};

my $title = "\u$name_singular Search Results";

my @header = ();
my @fields = ();
unless ( $opt{'disable_by'} ) {
  push @header, 'By';
  push @fields, sub { my $o = shift->otaker;
                      $o = 'auto billing'          if $o eq 'fs_daily';
                      $o = 'customer self-service' if $o eq 'fs_selfservice';
                      $o;
                    };
}

push @header, @{ $opt{'addl_header'} }
  if $opt{'addl_header'};
push @fields, @{ $opt{'addl_fields'} }
  if $opt{'addl_fields'};

my( $count_query, $sql_query );
if ( $cgi->param('magic') ) {

  my @search = ();
  my $orderby;
  if ( $cgi->param('magic') eq '_date' ) {


    if ( $cgi->param('agentnum') && $cgi->param('agentnum') =~ /^(\d+)$/ ) {
      push @search, "agentnum = $1"; # $search{'agentnum'} = $1;
      my $agent = qsearchs('agent', { 'agentnum' => $1 } );
      die "unknown agentnum $1" unless $agent;
      $title = $agent->agent. " $title";
    }

    if ( $cgi->param('custnum') =~ /^(\d+)$/ ) {
      push @search, "custnum = $1";
    }

    if ( $cgi->param('payby') ) {
      $cgi->param('payby') =~
        /^(CARD|CHEK|BILL|PREP|CASH|WEST|MCRD)(-(VisaMC|Amex|Discover|Maestro))?$/
          or die "illegal payby ". $cgi->param('payby');
      push @search, "cust_$thing.payby = '$1'";
      if ( $3 ) {

        my $cardtype = $3;

        my $search;
        if ( $cardtype eq 'VisaMC' ) {
          #avoid posix regexes for portability
          $search =
            " ( (     substring(cust_$thing.payinfo from 1 for 1) = '4'     ".
            "     AND substring(cust_$thing.payinfo from 1 for 4) != '4936' ".
            "     AND substring(cust_$thing.payinfo from 1 for 6)           ".
            "         NOT SIMILAR TO '49030[2-9]'                        ".
            "     AND substring(cust_$thing.payinfo from 1 for 6)           ".
            "         NOT SIMILAR TO '49033[5-9]'                        ".
            "     AND substring(cust_$thing.payinfo from 1 for 6)           ".
            "         NOT SIMILAR TO '49110[1-2]'                        ".
            "     AND substring(cust_$thing.payinfo from 1 for 6)           ".
            "         NOT SIMILAR TO '49117[4-9]'                        ".
            "     AND substring(cust_$thing.payinfo from 1 for 6)           ".
            "         NOT SIMILAR TO '49118[1-2]'                        ".
            "   )".
            "   OR substring(cust_$thing.payinfo from 1 for 2) = '51' ".
            "   OR substring(cust_$thing.payinfo from 1 for 2) = '52' ".
            "   OR substring(cust_$thing.payinfo from 1 for 2) = '53' ".
            "   OR substring(cust_$thing.payinfo from 1 for 2) = '54' ".
            "   OR substring(cust_$thing.payinfo from 1 for 2) = '54' ".
            "   OR substring(cust_$thing.payinfo from 1 for 2) = '55' ".
            "   OR substring(cust_$thing.payinfo from 1 for 2) = '36' ". #Diner's int'l processed as Visa/MC inside US
            " ) ";
        } elsif ( $cardtype eq 'Amex' ) {
          $search =
            " (    substring(cust_$thing.payinfo from 1 for 2 ) = '34' ".
            "   OR substring(cust_$thing.payinfo from 1 for 2 ) = '37' ".
            " ) ";
        } elsif ( $cardtype eq 'Discover' ) {
          $search =
            " (    substring(cust_$thing.payinfo from 1 for 4 ) = '6011'  ".
            "   OR substring(cust_$thing.payinfo from 1 for 2 ) = '65'    ".
            "   OR substring(cust_$thing.payinfo from 1 for 3 ) = '622'   ". #China Union Pay processed as Discover outside CN
            " ) ";
        } elsif ( $cardtype eq 'Maestro' ) { 
          $search =
            " (    substring(cust_$thing.payinfo from 1 for 2 ) = '63'     ".
            "   OR substring(cust_$thing.payinfo from 1 for 2 ) = '67'     ".
            "   OR substring(cust_$thing.payinfo from 1 for 6 ) = '564182' ".
            "   OR substring(cust_$thing.payinfo from 1 for 4 ) = '4936'   ".
            "   OR substring(cust_$thing.payinfo from 1 for 6 )            ".
            "      SIMILAR TO '49030[2-9]'                             ".
            "   OR substring(cust_$thing.payinfo from 1 for 6 )            ".
            "      SIMILAR TO '49033[5-9]'                             ".
            "   OR substring(cust_$thing.payinfo from 1 for 6 )            ".
            "      SIMILAR TO '49110[1-2]'                             ".
            "   OR substring(cust_$thing.payinfo from 1 for 6 )            ".
            "      SIMILAR TO '49117[4-9]'                             ".
            "   OR substring(cust_$thing.payinfo from 1 for 6 )            ".
            "      SIMILAR TO '49118[1-2]'                             ".
            " ) ";
        } else {
          die "unknown card type $cardtype";
        }

        my $masksearch = $search;
        $masksearch =~ s/cust_$thing\.payinfo/cust_$thing.paymask/gi;

        push @search,
          "( $search OR ( cust_$thing.paymask IS NOT NULL AND $masksearch ) )";

      }
    }

    if ( $cgi->param('payinfo') ) {
      $cgi->param('payinfo') =~ /^\s*(\d+)\s*$/
        or die "illegal payinfo ". $cgi->param('payinfo');
      push @search, "cust_$thing.payinfo = '$1'";
    }

    #for cust_pay_pending...  statusNOT=done
    if ( $cgi->param('statusNOT') =~ /^(\w+)$/ ) {
      push @search, "status != '$1'";
    }

    my($beginning, $ending) = FS::UI::Web::parse_beginning_ending($cgi);
    push @search, "_date >= $beginning ",
                  "_date <= $ending";

    push @search, FS::UI::Web::parse_lt_gt($cgi, $amount_field );

    $orderby = '_date';

  } elsif ( $cgi->param('magic') eq 'paybatch' ) {

    $cgi->param('paybatch') =~ /^([\w\/\:\-\.]+)$/
      or die "illegal paybatch: ". $cgi->param('paybatch');

    push @search, "paybatch = '$1'";

    $orderby = "LOWER(company || ' ' || last || ' ' || first )";

  } else {
    die "unknown search magic: ". $cgi->param('magic');
  }

  #here is the agent virtualization
  push @search, $curuser->agentnums_sql;

  my $search = ' WHERE '. join(' AND ', @search);

  $count_query = "SELECT COUNT(*), SUM($amount_field) ".
                 "FROM cust_$thing LEFT JOIN cust_main USING ( custnum )".
                 $search;

  $sql_query = {
    'table'     => "cust_$thing",
    'select'    => join(', ',
                     "cust_$thing.*",
                     'cust_main.custnum as cust_main_custnum',
                     FS::UI::Web::cust_sql_fields(),
                   ),
    'hashref'   => {},
    'extra_sql' => "$search ORDER BY $orderby",
    'addl_from' => 'LEFT JOIN cust_main USING ( custnum )',
  };

} else {

  #hmm... is this still used?

  $cgi->param('payinfo') =~ /^\s*(\d+)\s*$/ or die "illegal payinfo";
  my $payinfo = $1;

  $cgi->param('payby') =~ /^(\w+)$/ or die "illegal payby";
  my $payby = $1;

  $count_query = "SELECT COUNT(*), SUM($amount_field) FROM cust_$thing".
                 "  WHERE payinfo = '$payinfo' AND payby = '$payby'".
                 "  AND ". $curuser->agentnums_sql;

  $sql_query = {
    'table'     => "cust_$thing",
    'hashref'   => { 'payinfo' => $payinfo,
                     'payby'   => $payby    },
    'extra_sql' => $curuser->agentnums_sql.
                   " ORDER BY _date",
  };

}

my $link = '';
if (    ( $curuser->access_right('View invoices') #XXX for now
          || $curuser->access_right('View customer payments')
        )
     && ! $opt{'disable_link'}
   )
{
  $link = [ "${p}view/cust_$thing.html?${thing}num=", $thing.'num' ]
}

my $cust_link = sub {
  my $cust_thing = shift;
  $cust_thing->cust_main_custnum
    ? [ "${p}view/cust_main.cgi?", 'custnum' ] 
    : '';
};

</%init>
