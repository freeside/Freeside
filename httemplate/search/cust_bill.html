<% include( 'elements/search.html',
                 'title'       => 'Invoice Search Results',
                 'html_init'   => $html_init,
                 'menubar'     => $menubar,
                 'name'        => 'invoices',
                 'query'       => $sql_query,
                 'count_query' => $count_query,
                 'count_addl'  => $count_addl,
                 'redirect'    => $link,
                 'header'      => [ 'Invoice #',
                                    'Balance',
                                    'Net Amount',
                                    'Gross Amount',
                                    'Date',
                                    FS::UI::Web::cust_header(),
                                  ],
                 'fields'      => [
                   'invnum',
                   sub { sprintf($money_char.'%.2f', shift->get('owed') ) },
                   sub { sprintf($money_char.'%.2f', shift->get('net') ) },
                   sub { sprintf($money_char.'%.2f', shift->charged     ) },
                   sub { time2str('%b %d %Y', shift->_date ) },
                   \&FS::UI::Web::cust_fields,
                 ],
                 'align' => 'rrrr'.FS::UI::Web::cust_aligns(),
                 'links' => [
                   $link,
                   $link,
                   $link,
                   $link,
                   $link,
                   ( map { $_ ne 'Cust. Status' ? $clink : '' }
                         FS::UI::Web::cust_header()
                   ),
                 ],
                 'color' => [ 
                              '',
                              '',
                              '',
                              '',
                              '',
                              FS::UI::Web::cust_colors(),
                            ],
                 'style' => [ 
                              '',
                              '',
                              '',
                              '',
                              '',
                              FS::UI::Web::cust_styles(),
                            ],

  
      )
%>
<%init>

die "access denied"
  unless $FS::CurrentUser::CurrentUser->access_right('List invoices');

my $join_cust_main = 'LEFT JOIN cust_main USING ( custnum )';
#here is the agent virtualization
my $agentnums_sql = $FS::CurrentUser::CurrentUser->agentnums_sql;

my( $count_query, $sql_query );
my $count_addl = '';
#my $distinct = '';
my %search;

if ( $cgi->param('invnum') =~ /^\s*(FS-)?(\d+)\s*$/ ) {

  $count_query =
    "SELECT COUNT(*) FROM cust_bill $join_cust_main".
    "  WHERE invnum = $2 AND $agentnums_sql"; #agent virtualization
  $sql_query = {
    'table'     => 'cust_bill',
    'addl_from' => $join_cust_main,
    'hashref'   => { 'invnum' => $2 },
    #'select'    => '*',
    'extra_sql' => " AND $agentnums_sql", #agent virtualization
  };

} else {

  #some false laziness w/cust_bill::re_X
  my @where;
  my $orderby = 'ORDER BY cust_bill._date';

  if ( $cgi->param('beginning')
       && $cgi->param('beginning') =~ /^([ 0-9\-\/]{0,10})$/ ) {
    $search{'begin'} = str2time($1);
  }
  if ( $cgi->param('ending')
        && $cgi->param('ending') =~ /^([ 0-9\-\/]{0,10})$/ ) {
    $search{'end'} = str2time($1) + 86399;
  }

  if ( $cgi->param('begin') =~ /^(\d+)$/ ) {
    $search{'begin'} = $1;
  }
  if ( $cgi->param('end') =~ /^(\d+)$/ ) {
    $search{'end'} = $1;
  }

  if ( $cgi->param('invnum_min') =~ /^\s*(\d+)\s*$/ ) {
    $search{'invnum_min'} = $1;
  }
  if ( $cgi->param('invnum_max') =~ /^\s*(\d+)\s*$/ ) {
    $search{'invnum_max'} = $1;
  }

  if ( $cgi->param('agentnum') =~ /^(\d+)$/ ) {
    $search{'agentnum'} = $1;
  }

  $search{'open'} = 1 if $cgi->param('open');
  $search{'net'}  = 1 if $cgi->param('net' );

  my($query) = $cgi->keywords;
  if ( $query =~ /^(OPEN(\d*)_)?(invnum|date|custnum)$/ ) {
    $search{'open'} = 1 if $1;
    ($search{'days'}, my $field) = ($2, $3);
    $field = "_date" if $field eq 'date';
    $orderby = "ORDER BY cust_bill.$field";
  }

  if ( $cgi->param('newest_percust') ) {
    $search{'newest_percust'} = 1;
    $count_query = "SELECT COUNT(DISTINCT cust_bill.custnum), 'N/A', 'N/A'";
  }

  my $extra_sql = ' WHERE '. FS::cust_bill->search_sql( \%search );

  unless ( $count_query ) {
    $count_query = 'SELECT COUNT(*), '. join(', ',
                     map "SUM($_)",
                         ( 'charged',
                           FS::cust_bill->net_sql,
                           FS::cust_bill->owed_sql,
                         )
                   );
    $count_addl = [ '$%.2f invoiced (gross)',
                    '$%.2f invoiced (net)',
                    '$%.2f outstanding balance',
                  ];
  }
  $count_query .=  " FROM cust_bill $join_cust_main $extra_sql";

  $sql_query = {
    'table'     => 'cust_bill',
    'addl_from' => $join_cust_main,
    'hashref'   => {},
    #'select'    => "$distinct ". join(', ',
    'select'    => join(', ',
                     'cust_bill.*',
                     #( map "cust_main.$_", qw(custnum last first company) ),
                     'cust_main.custnum as cust_main_custnum',
                     FS::UI::Web::cust_sql_fields(),
                     FS::cust_bill->owed_sql. ' AS owed',
                     FS::cust_bill->net_sql.  ' AS net',
                   ),
    'extra_sql' => $extra_sql,
    'order_by'  => $orderby,
  };

}

my $link  = [ "${p}view/cust_bill.cgi?", 'invnum', ];
my $clink = sub {
  my $cust_bill = shift;
  $cust_bill->cust_main_custnum
    ? [ "${p}view/cust_main.cgi?", 'custnum' ]
    : '';
};

my $conf = new FS::Conf;
my $money_char = $conf->config('money_char') || '$';

my $html_init = join("\n", map {
 ( my $action = $_ ) =~ s/_$//;
 include('/elements/progress-init.html',
           $_.'form',
           [ keys %search ],
           "../misc/${_}invoices.cgi",
           { 'message' => "Invoices re-${action}ed" }, #would be nice to show the number of them, but...
           $_, #key
        ),
 qq!<FORM NAME="${_}form">!,
 ( map qq!<INPUT TYPE="hidden" NAME="$_" VALUE="$search{$_}">!, keys %search ),
 qq!</FORM>!
} qw( print_ email_ fax_ ftp_ spool_ ) ). 

'<SCRIPT TYPE="text/javascript">

function confirm_print_process() {
  if ( ! confirm("Are you sure you want to reprint these invoices?") ) {
    return;
  }
  print_process();
}
function confirm_email_process() {
  if ( ! confirm("Are you sure you want to re-email these invoices?") ) {
    return;
  }
  email_process();
}
function confirm_fax_process() {
  if ( ! confirm("Are you sure you want to re-fax these invoices?") ) {
    return;
  }
  fax_process();
}
function confirm_ftp_process() {
  if ( ! confirm("Are you sure you want to re-FTP these invoices?") ) {
    return;
  }
  ftp_process();
}
function confirm_spool_process() {
  if ( ! confirm("Are you sure you want to re-spool these invoices?") ) {
    return;
  }
  spool_process();
}

</SCRIPT>';

my $menubar = [];

if ( $FS::CurrentUser::CurrentUser->access_right('Resend invoices') ) {

  push @$menubar, 'Print these invoices' =>
                    "javascript:confirm_print_process()",
                  'Email these invoices' =>
                    "javascript:confirm_email_process()";

  push @$menubar, 'Fax these invoices' =>
                    "javascript:confirm_fax_process()"
    if $conf->exists('hylafax');

  push @$menubar, 'FTP these invoices' =>
                    "javascript:confirm_ftp_process()"
    if $conf->exists('cust_bill-ftpformat');

  push @$menubar, 'Spool these invoices' =>
                    "javascript:confirm_spool_process()"
    if $conf->exists('cust_bill-spoolformat');

}

</%init>
