<& elements/search.html,
  'title'       => emt('Invoice Search Results'),
  'html_init'   => $html_init,
  'menubar'     => $menubar,
  'name'        => 'invoices',
  'query'       => $sql_query,
  'count_query' => $count_query,
  'count_addl'  => $count_addl,
  'redirect'    => $link,
  'header'      => [ emt('Invoice #'),
                     emt('Gross Amount'),
                     emt('Discount'),
                     emt('Credits'),
                     emt('Net Amount'),
                     emt('Balance'),
                     emt('Date'),
                     FS::UI::Web::cust_header(),
                   ],
  'fields'      => [
    'display_invnum',
    'gross',
    'discounted',
    'credited',
    'net',
    'owed',
    sub { time2str('%b %d %Y', shift->_date ) },
    \&FS::UI::Web::cust_fields,
  ],
  'sort_fields' => [
    'COALESCE( agent_invid, invnum )',
    'gross',
    'discounted',
    'credited',
    'net',
    'owed',
    '_date',
  ],
  'format' => [
    '',
    $money_char.'%.2f',
    $money_char.'%.2f',
    $money_char.'%.2f',
    $money_char.'%.2f',
    $money_char.'%.2f',
    '',
  ],
  'align' => 'rrrrrrl'.FS::UI::Web::cust_aligns(),
  'links' => [
    $link,
    $link,
    $link,
    $link,
    $link,
    $link,
    $link,
    ( map { $_ ne 'Cust. Status' ? $clink : '' }
          FS::UI::Web::cust_header()
    ),
  ],
  'color' => [ 
               '',
               '',
               '',
               '',
               '',
               '',
               '',
               FS::UI::Web::cust_colors(),
             ],
  'style' => [ 
               '',
               '',
               '',
               '',
               '',
               '',
               '',
               FS::UI::Web::cust_styles(),
             ],
&>
<%init>

die "access denied"
  unless $FS::CurrentUser::CurrentUser->access_right('List invoices');

my( $count_query, $sql_query );
my $count_addl = '';
my %search = ();

if ( $cgi->param('invnum') =~ /^\s*(FS-)?(\d+)\s*$/ ) {

  my $join_cust_main = FS::UI::Web::join_cust_main('cust_bill');
  #here is the agent virtualization
  my $agentnums_sql = $FS::CurrentUser::CurrentUser->agentnums_sql;

  my $invnum_or_invid = "( invnum = $2 OR agent_invid = $2 )";
  my $where = "WHERE $invnum_or_invid AND $agentnums_sql";
  
  $count_query = "SELECT COUNT(*) FROM cust_bill $join_cust_main $where";

  $sql_query = {
    'table'     => 'cust_bill',
    'addl_from' => $join_cust_main,
    'hashref'   => {},
    'extra_sql' => $where,
  };

  if ( FS::Record->scalar_sql($count_query) == 0 ) {

    # check for a voided invoice
    $count_query =~ s/cust_bill/cust_bill_void/g;
    if ( FS::Record->scalar_sql($count_query) > 0 ) {
      # Redirect to the void search.
      my $url = $cgi->self_url;
      $url =~ s(search/cust_bill)(search/cust_bill_void);
      $m->clear_buffer;
      $m->print($cgi->redirect($url));
      $m->abort;
    }
  }

} else {

  #deal with some old-style URLs
  my($query) = $cgi->keywords;
  if ( $query =~ /^(OPEN(\d*)_)?(invnum|date|custnum)$/ ) {
    $search{'open'} = 1 if $1;
    ($search{'days'}, my $field) = ($2, $3);
    $field = "_date" if $field eq 'date';
    $search{'order_by'} = "cust_bill.$field";
  }

  #scalars
  for (qw( agentnum custnum cust_status refnum invnum_min invnum_max
           open net newest_percust 

      )) 
  {
    $search{$_} = $cgi->param($_) if length($cgi->param($_));
  }

  #arrays
  for my $param (qw( cust_classnum payby )) {
  $search{$param} = [ $cgi->param($param) ]
    if grep { $_ eq $param } $cgi->param;
  }

  #amounts (range)
  $search{$_} = [ FS::UI::Web::parse_lt_gt($cgi, $_) ]
    foreach qw( charged owed );

  my($beginning, $ending) = FS::UI::Web::parse_beginning_ending($cgi, '');
  $search{'_date'} = [ $beginning, $ending ]
    unless $beginning == 0 && $ending == 4294967295;

  # promised date
  my $start_of_day = timelocal(0, 0, 0, (localtime(time))[3,4,5]);
  foreach ( $cgi->param('promised_date') ) {
    # only if at least one box is checked
    $search{promised_date} ||= [ $start_of_day, $start_of_day, 0 ];
    if ($_ eq 'past') {
      # accept everything before today
      $search{promised_date}[0] = 0;
    }
    elsif ( $_ eq 'future' ) {
      # accept everything after today
      $search{promised_date}[1] = 4294967295;
    }
    elsif ( $_ eq 'null' ) {
      # accept nulls
      $search{promised_date}[2] = 1;
    }
  }

  $sql_query   = FS::cust_bill->search(\%search);
  $count_query = delete( $sql_query->{'count_query'} );
  $count_addl  = delete( $sql_query->{'count_addl'}  );

}

my $link  = [ "${p}view/cust_bill.cgi?", 'invnum', ];
my $clink = sub {
  my $cust_bill = shift;
  $cust_bill->cust_main_custnum
    ? [ "${p}view/cust_main.cgi?", 'custnum' ]
    : '';
};

my $conf = new FS::Conf;
my $money_char = $conf->config('money_char') || '$';

my $html_init = join("\n", map {
 ( my $action = $_ ) =~ s/_$//;
 include('/elements/progress-init.html',
           $_.'form',
           [ keys %search ],
           "../misc/${_}invoices.cgi",
           { 'message' => "Invoices re-${action}ed" }, #would be nice to show the number of them, but...
           $_, #key
        ),
 qq!<FORM NAME="${_}form">!,
 ( map { my $f = $_;
         my @values = ref($search{$f}) ? @{ $search{$f} } : $search{$f};
         map qq!<INPUT TYPE="hidden" NAME="$f" VALUE="$_">!, @values;
       }
       keys %search
 ),
 qq!</FORM>!
} qw( print_ email_ fax_ ftp_ spool_ ) ). 

'<SCRIPT TYPE="text/javascript">

function confirm_print_process() {
  if ( ! confirm('.js_mt("Are you sure you want to reprint these invoices?").') ) {
    return;
  }
  print_process();
}
function confirm_email_process() {
  if ( ! confirm('.js_mt("Are you sure you want to re-email these invoices?").') ) {
    return;
  }
  email_process();
}
function confirm_fax_process() {
  if ( ! confirm('.js_mt("Are you sure you want to re-fax these invoices?").') ) {
    return;
  }
  fax_process();
}
function confirm_ftp_process() {
  if ( ! confirm('.js_mt("Are you sure you want to re-FTP these invoices?").') ) {
    return;
  }
  ftp_process();
}
function confirm_spool_process() {
  if ( ! confirm('.js_mt("Are you sure you want to re-spool these invoices?").') ) {
    return;
  }
  spool_process();
}

</SCRIPT>';

my $menubar = [];

if ( $FS::CurrentUser::CurrentUser->access_right('Resend invoices') ) {

  push @$menubar, emt('Print these invoices') =>
                    "javascript:confirm_print_process()",
                  emt('Email these invoices') =>
                    "javascript:confirm_email_process()";

  push @$menubar, emt('Fax these invoices') =>
                    "javascript:confirm_fax_process()"
    if $conf->exists('hylafax');

  push @$menubar, emt('FTP these invoices') =>
                    "javascript:confirm_ftp_process()"
    if $conf->exists('cust_bill-ftpformat');

  push @$menubar, emt('Spool these invoices') =>
                    "javascript:confirm_spool_process()"
    if $conf->exists('cust_bill-spoolformat');

}

</%init>
