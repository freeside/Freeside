<% include( 'elements/search.html',
               'title' => $title,
               'name'  => 'call detail records',

               'query' => { 'table'     => 'cdr',
                            'hashref'   => $hashref,
			    'extra_sql' => $qsearch,
			    'order_by'  => 'ORDER BY calldate',
                          },
               'count_query' => $count_query,
               'count_addl' => [ $totalminutes_sub ],
               'header' => [
                             '', # checkbox column
                             @header,
                           ],
               'fields' => [
                             sub {
                               return '' unless $edit_data;
                               $areboxes = 1;
                               my $cdr = shift;
                               my $acctid = $cdr->acctid;
                               qq!<INPUT NAME="acctid$acctid" TYPE="checkbox" VALUE="1">!;
                             },
                             @fields,       #XXX fill in some pretty-print
                                            #processing, etc.
                           ],
               'links' => \@links,

               'html_form'   => qq!<FORM NAME="cdrForm" ACTION="$p/misc/cdr.cgi" METHOD="POST">!,
               #false laziness w/queue.html
               'html_foot' => sub {
                                if ( $areboxes ) {
                                  '<BR><INPUT TYPE="button" VALUE="select all" onClick="setAll(true)">'.
                                  '<INPUT TYPE="button" VALUE="unselect all" onClick="setAll(false)">'.
                                  qq!<BR><INPUT TYPE="submit" NAME="action" VALUE="reprocess selected" onClick="return confirm('Are you sure you want to reprocess the selected CDRs?')">!.
                                  qq!<INPUT TYPE="submit" NAME="action" VALUE="delete selected" onClick="return confirm('Are you sure you want to delete the selected CDRs?')"><BR>!.
                                  '<SCRIPT TYPE="text/javascript">'.
                                  '  function setAll(setTo) { '.
                                  '    theForm = document.cdrForm;'.
                                  '    for (i=0,n=theForm.elements.length;i<n;i++)'.
                                  '      if (theForm.elements[i].name.indexOf("acctid") != -1)'.
                                  '        theForm.elements[i].checked = setTo;'.
                                  '  }'.
                                  '</SCRIPT>';
                                } else {
                                  '';
                                }
                              },
             )
%>
<%init>

die "access denied"
  unless $FS::CurrentUser::CurrentUser->access_right('List rating data');

my $edit_data = $FS::CurrentUser::CurrentUser->access_right('Edit rating data');

my $totalminutes_sub = sub {
    my $billsec = shift;
    sprintf("%.2f",$billsec/60) . ' total minutes';
};

my $conf = new FS::Conf;

my $areboxes = 0;

my $title = 'Call Detail Records';
my $hashref = {};

#process params for CDR search, populate $hashref...
# and fixup $count_query

my @search = ();

###
# dates
###

my $str2time_sql = str2time_sql;
my $closing      = str2time_sql_closing;

my($beginning, $ending) = FS::UI::Web::parse_beginning_ending($cgi);
push @search, "$str2time_sql calldate $closing >= $beginning ",
              "$str2time_sql calldate $closing <= $ending";

###
# duration / billsec
###

push @search, FS::UI::Web::parse_lt_gt($cgi, 'duration');
push @search, FS::UI::Web::parse_lt_gt($cgi, 'billsec');

#above here things just push @search
#below here things also have to define $hashref->{} or push @qsearch
my @qsearch = @search;

###
# freesidestatus
###

if ( $cgi->param('freesidestatus') eq 'NULL' ) {

  $title = "Unprocessed $title";
  $hashref->{'freesidestatus'} = ''; # Record.pm will take care of it
  push @search, "( freesidestatus IS NULL OR freesidestatus = '' )";

} elsif ( $cgi->param('freesidestatus') =~ /^([\w ]+)$/ ) {

  $title = "Processed $title";
  $hashref->{'freesidestatus'} = $1;
  push @search, "freesidestatus = '$1'";

}

###
# termpartNstatus
###

foreach my $param ( grep /^termpart\d+status$/, $cgi->param ) {

  my $status = $cgi->param($param);

  $param =~ /^termpart(\d+)status$/ or die 'guru meditation 54something';
  my $termpart = $1;

  my $search = '';
  if ( $status eq 'NULL' ) {

    #false lazienss w/cdr_termination.pm (i should be a part_termination method)
    my $where_term =
      "( cdr.acctid = cdr_termination.acctid AND termpart = $termpart ) ";
    #my $join_term = "LEFT JOIN cdr_termination ON ( $where_term )";
    $search =
      "NOT EXISTS ( SELECT 1 FROM cdr_termination WHERE $where_term )";

  } elsif ( $status =~ /^([\w ]+)$/ ) {

    #false lazienss w/cdr_termination.pm (i should be a part_termination method)
    my $where_term =
      "( cdr.acctid = cdr_termination.acctid AND termpart = $termpart AND status = '$1' ) ";
    #my $join_term = "LEFT JOIN cdr_termination ON ( $where_term )";
    $search =
      "EXISTS ( SELECT 1 FROM cdr_termination WHERE $where_term )";

  }

  if ( $search ) {
    push @search,  $search;
    push @qsearch, $search;
  }

}

###
# src/dest/charged_party/svcnum
###

my $phonenum = qr/^\s*([\d\-\+\ ]+)\s*$/;
my $x = qr/\D/;
if ( $conf->exists('svc_phone-allow_alpha_phonenum') ) {
  $phonenum = qr/^\s*([\d\-\+\ A-Za-z]+)\s*$/;
  $x = qr/[^\dA-Za-z]/;
}

if ( $cgi->param('src') =~ $phonenum ) {
  ( my $src = $1 ) =~ s/$x//g;
  $hashref->{'src'} = $src;
  push @search, "src = '$src'";
}

if ( $cgi->param('dst') ) {

  my @d = map { $_, "1$_" } split(/\s*,\s*/, $cgi->param('dst') );
  
  my $search = 'dst IN ('. join(',', map dbh->quote($_), @d). ')';

  push @search,  $search;
  push @qsearch, $search;

}

if ( $cgi->param('dcontext') =~ /^\s*(.+)\s*$/ ) {
  my $dcontext = $1;
  $hashref->{'dcontext'} = $dcontext;
  push @search, 'dcontext = '. dbh->quote($dcontext);
}

if ( $cgi->param('charged_party') ) {

  my @cp = map { $_, "1$_" }
             split(/\s*,\s*/, $cgi->param('charged_party') );
  
  my $search = 'charged_party IN ('. join(',', map dbh->quote($_), @cp). ')';

  push @search,  $search;
  push @qsearch, $search;
}

if ( $cgi->param('charged_party_or_src') ) {

  my @cp = map { $_, "1$_" }
             split(/\s*,\s*/, $cgi->param('charged_party_or_src') );
  my $in = join(',', map dbh->quote($_), @cp);

  my $search = "( charged_party IN ($in) OR src IN ($in) )";

  push @search,  $search;
  push @qsearch, $search;
}

if ( $cgi->param('lastapp') =~ /^\s*(.+)\s*$/ ) {
  my $lastapp = $1;
  $hashref->{'lastapp'} = $lastapp;
  push @search, 'lastapp = '. dbh->quote($lastapp);
}

if ( $cgi->param('svcnum') =~ /^([\d, ]+)$/ ) {
  my $svcnum = $1;
  my $search = "svcnum IN ($svcnum)";
  push @search,  $search;
  push @qsearch, $search;
}

###
# cdrbatchnum (or legacy cdrbatch)
###

if ( $cgi->param('cdrbatch') ) {

  my $cdr_batch =
    qsearchs('cdr_batch', { 'cdrbatch' => scalar($cgi->param('cdrbatch')) } );
  if ( $cdr_batch ) {
    $hashref->{cdrbatchnum} = $cdr_batch->cdrbatchnum;
    push @search, 'cdrbatchnum = '. $cdr_batch->cdrbatchnum;
  } else {
    die "unknown cdrbatch ". $cgi->param('cdrbatch');
  }

} elsif ( $cgi->param('cdrbatchnum') ne '__ALL__' ) {

  if ( $cgi->param('cdrbatchnum') eq '' ) {
    my $search = "( cdrbatchnum IS NULL )";
    push @qsearch, $search;
    push @search,  $search;
  } elsif ( $cgi->param('cdrbatchnum') =~ /^(\d+)$/ ) {
    $hashref->{cdrbatchnum} = $1;
    push @search, "cdrbatchnum = $1";
  }

}

###
# acctid
###

if ( $cgi->param('acctid') =~ /\d/ ) {
  my $acctid = $cgi->param('acctid');
  $acctid =~ s/\r\n/\n/g; #browsers?
  my @acctid = map  { /^\s*(\d+)\s*$/ or die "guru meditation #4"; $1; }
               grep { /^\s*(\d+)\s*$/ }
               split(/\n/, $acctid);
  if ( @acctid ) {
    my $search = 'acctid IN ( '. join(',', @acctid). ' )';
    push @qsearch, $search;
    push @search,  $search;
  }
}

###
# finish it up
###

my $search = join(' AND ', @search);
$search = "WHERE $search" if $search;

my $count_query = "SELECT COUNT(*), sum(billsec) FROM cdr $search";

my $qsearch = join(' AND ', @qsearch);
$qsearch = ( scalar(keys %$hashref) ? ' AND ' : ' WHERE ' ) . $qsearch
  if $qsearch;

###
# display fields
###

my %header = %{ FS::cdr->table_info->{'fields'} };

my @first = qw( acctid calldate clid charged_party src dst dcontext );
my %first = map { $_=>1 } @first;

my @fields = ( @first, grep !$first{$_}, fields('cdr') );

if ( $cgi->param('show') ) {
  @fields = grep $cgi->param("show_$_"), @fields;
}

my @header = map {
                   if ( exists($header{$_}) ) {
                     $header{$_};
                   } else {
                     my $header = $_;
                     $header =~ s/\_/ /g; #//wtf
                     ucfirst($header);
                   }
                 } @fields;

my $date_sub_factory = sub {
  my $column = shift;
  sub {
    #my $cdr = shift;
    my $date = shift->$column();
    $date ? time2str( '%Y-%m-%d %T', $date ) : ''; #config time2str format?
  };
};

my %fields = (
  #any other formatters?
  map { $_ => &{ $date_sub_factory }($_) } qw( startdate answerdate enddate )
);

my %links = (
  'svcnum' =>
    sub { $_[0]->svcnum ? [ $p.'view/svc_phone.cgi?', 'svcnum' ] : ''; },
);

@fields = map { exists($fields{$_}) ? $fields{$_} : $_ } @fields;

              #checkbox column
my @links = ( '', map { exists($links{$_}) ? $links{$_} : '' } @fields );

</%init>
