<%doc>

Example:

  include('elements/report.html',
    #required
    'title'           => 'Page title',
    'items'           => \@items,
    'data'            => [ \@item1 \@item2 ... ],

    #these run parallel to items, and can be given as hashes
    'row_labels'      => \@row_labels,    #required
    'colors'          => \@colors,        #required
    'graph_labels'    => \@graph_labels,  #defaults to row_labels

    'links'           => \@links,         #optional

    #these run parallel to the elements of each @item
    'col_labels'      => \@col_labels,    #required
    'axis_labels'     => \@axis_labels,   #defaults to col_labels

    #optional
    'nototal'         => 1,
    'graph_type'      => 'LinesPoints',
    'bottom_total'    => 1,
    'sprintf'         => '%u', #sprintf format, overrides default %.2f
    'disable_money'   => 1,
  );

About @links: Each element must be an arrayref, corresponding to an element of
@items.  Within the array, the first element is a URL prefix, and the rest 
are suffixes corresponding to data elements.  These will be joined without 
any delimiter and linked from the elements in @data.

</%doc>
% if ( $cgi->param('_type') =~ /^(csv)$/ ) {
%
%   #http_header('Content-Type' => 'text/comma-separated-values' ); #IE chokes
%   #http_header('Content-Type' => 'text/plain' );
%   http_header('Content-Type' => 'text/csv');
%   http_header('Content-Disposition' => "attachment;filename=$filename.csv");
%
%   my $csv = new Text::CSV_XS { 'always_quote' => 1,
%                                'eol'          => "\n", #"\015\012", #"\012"
%                              };
%
%   $csv->combine('', @col_labels, $opt{'nototal'} ? () : 'Total');
%   
<% $csv->string %>
%
%   my @bottom_total = ();
%   foreach ( @items ) {
%
%     my $col = 0;
%     my $total = 0;
%     $csv->combine(
%       shift( @row_labels ),
%       map { $total += $_; $bottom_total[$col++] += $_; sprintf($sprintf, $_); }
%         ( @{ shift( @data ) } ),
%       ( $opt{'nototal'} ? () : sprintf($sprintf, $total) ),
%     );
%     unless ( $opt{'nototal'} ) { 
%       $bottom_total[$col++] += $total; 
%     } 
<% $csv->string %>
%
%   }
% 
%   if ( $opt{'bottom_total'} ) {
%     $csv->combine(
%       'Total',
%       map { sprintf($sprintf, $_) } @bottom_total,
%     );
%
<% $csv->string %>
%
%   } 
%   
% } elsif ( $cgi->param('_type') =~ /(xls)$/ ) {
%
%   #http_header('Content-Type' => 'application/excel' ); #eww
%   http_header('Content-Type' => 'application/vnd.ms-excel' );
%   #http_header('Content-Type' => 'application/msexcel' ); #alas
%   http_header('Content-Disposition' => "attachment;filename=$filename.xls");
%
%   my $output = '';
%   my $XLS = new IO::Scalar \$output;
%   my $workbook = Spreadsheet::WriteExcel->new($XLS)
%     or die "Error opening .xls file: $!";
%
%   my $worksheet = $workbook->add_worksheet(substr($opt{'title'},0,31));
%
%   my($r,$c) = (0,0);
%
%   foreach ('', @col_labels, ($opt{'nototal'} ? () : 'Total') ) {
%     my $header = $_;
%     $worksheet->write($r, $c++, $header)
%   }
%
%   my @bottom_total = ();
%   foreach ( @items ) {
%     $r++;
%     $c = 0;
%     my $total = 0;
%     $worksheet->write( $r, $c++, shift( @row_labels ) );
%     foreach ( @{ shift( @data ) } ) {
%       $total += $_;
%       $bottom_total[$c-1] += $_;
%       $worksheet->write($r, $c++,  sprintf($sprintf, $_) );
%     }
%     unless ( $opt{'nototal'} ) { 
%       $bottom_total[$c-1] += $total; 
%       $worksheet->write($r, $c++,  sprintf($sprintf, $total) );
%     } 
%   }
% 
%   $c = 0;
%   if ( $opt{'bottom_total'} ) {
%     $r++;
%     $worksheet->write($r, $c++, 'Total');
%     $worksheet->write($r, $c++, sprintf($sprintf, $_)) foreach @bottom_total;
%   } 
%   
%   $workbook->close();# or die "Error creating .xls file: $!";
%
%   http_header('Content-Length' => length($output) );
%   
<% $output %>
% } elsif ( $cgi->param('_type') eq 'png' ) {
%
%   my $graph_type = 'LinesPoints';
%   if ( $opt{'graph_type'} =~ /^(LinesPoints|Mountain|Bars)$/ ) {
%     $graph_type = $1;
%   }
%   my $class = "Chart::$graph_type";
%
%   my $chart = $class->new(976,384);
% # the chart area itself is 900 pixels wide, and the date labels are ~60 each.
% # staggered, we can fit about 28 of them.
% # they're about 12 pixels high, so vertically, we can fit about 60 (allowing
% # space for them to be readable).
% # after that we have to start skipping labels. also remove the dots, since 
% # they're just a blob at that point.
%   my $num_labels = scalar(@{ $opt{axis_labels} });
%   my %chart_opt;
%   if ( $num_labels > 28 ) {
%     $chart_opt{x_ticks} = 'vertical';
%     if ( $num_labels > 60 ) {
%       $chart_opt{skip_x_ticks} = int($num_labels / 60) + 1;
%       $chart_opt{pt_size} = 1;
%     }
%   }
%   my $d = 0;
%   $chart->set(
%     #'min_val' => 0,
%     'legend' => 'bottom',
%     'colors' => { ( 
%                     map { my $color = $_;
%                           'dataset'.$d++ =>
%                             [ map hex($_), unpack 'a2a2a2', $color ]
%                         }
%                         @{ $opt{'colors'} }
%                   ),
%                   'grey_background' => 'white',
%                   'background' => [ 0xe8, 0xe8, 0xe8 ], #grey
%                 },
%     'legend_labels' => $opt{'graph_labels'},
%     'brush_size' => 4,
%     %chart_opt,
%   );
%
%   http_header('Content-Type' => 'image/png' );
%   http_header('Cache-Control' => 'no-cache' );
%
%   $chart->_set_colors();
%   
<% $chart->scalar_png([ $opt{'axis_labels'}, @data ]) %>
%
% } else {
% # image and download links should use the cached data
% # just directly reference this component
% my $myself = $p.'graph/elements/report.html?session='.$session;
%
<% include('/elements/header.html', $opt{'title'} ) %>
% unless ( $opt{'graph_type'} eq 'none' ) {

<IMG SRC="<% "$myself;_type=png" %>" WIDTH="976" HEIGHT="384"
 STYLE="page-break-after:always;">
% }
<P ALIGN="right" CLASS="noprint">

% unless ( $opt{'disable_download'} ) { 
            Download full results<BR>
            as <A HREF="<% "$myself;_type=xls" %>">Excel spreadsheet</A><BR>
            as <A HREF="<% "$myself;_type=csv" %>">CSV file</A></P>
% } 
%
</P>
%# indexed by item, then by entry (the element indices of @{$data[$i]}).
% my @cell = ();
% my @styles;
% my $num_entries = scalar(@col_labels);
% my $num_items = scalar(@items);
% $cell[0] = ['']; #top left corner
% foreach my $column ( @col_labels ) {
%   $column =~ s/ /\<BR\>/;
%   push @{$cell[0]}, $column;
% }
% if ( ! $opt{'nototal'} ) {
%   $num_entries++;
%   push @{$cell[0]}, emt('Total');
% }

% # i for item, e for entry

% my $i = 1;
% foreach my $row ( @items ) {
% #make a style
%   my $color = shift @{ $opt{'colors'} };
%   push @styles, ".i$i { text-align: right; color: #$color; }";
% #create the data row
%   my $links = shift @{$opt{'links'}} || [''];
%   my $link_prefix = shift @$links;
%   $link_prefix = '<A CLASS="cell" HREF="'.$link_prefix if $link_prefix;
%   my $label = shift @row_labels;
%   $cell[$i] = [ $label ];
%
%   my $data_row = shift @data;
%   if ( ! $opt{'nototal'} ) {
%     push @$data_row, sum(@$data_row);
%   }
%   foreach my $entry ( @$data_row ) {
%     $entry = $money_char . sprintf($sprintf, $entry);
%     $entry = $link_prefix . shift(@$links) . "\">$entry</A>" if $link_prefix;
%     push @{$cell[$i]}, $entry;
%   }
%   $i++;
% }
% if ( $opt{'bottom_total'} ) {
%   # it's an extra item
%   $num_items++;
%   push @styles, ".i$i { text-align: right; background-color: #f5f6be; }";
%   my $links = $opt{'bottom_link'} || [];
%   my $link_prefix = shift @$links;
%   $cell[$i] = [ emt('Total') ];
%   for (my $e = 0; $e < $num_entries + 1; $e++) {
%     my $entry = sum(map { $_->[$e] } @data);
%     $entry = $money_char . sprintf($sprintf, $entry);
%     $entry = $link_prefix . shift(@$links) . "\">$entry</A>" if $link_prefix;
%     push @{$cell[$i]}, $entry;
%   }
% }

<STYLE type="text/css">
a.cell {
  color: inherit !important;
}
td.cell {
  border-color: #000;
}
<% join("\n", @styles) %>
%# item labels
.e0 {
  text-align: center;
  font-weight: bold;
}
%# totals
% if ( ! $opt{'nototal'} ) {
.e<% $num_entries %> {
  text-align: right;
  background-color: #f5f6be;
}
% }
%# date labels
.i0 {
  text-align: center;
  font-weight: bold;
}
</STYLE>

<% include('/elements/table.html', 'f8f8f8') %>
% if ( $opt{'transpose'} ) {
%   for ( my $e = 0; $e < $num_entries + 1; $e++ ) {
  <TR>
%     for ( my $i = 0; $i < $num_items + 1; $i++ ) {
    <TD CLASS="<%"cell i$i e$e"%>"><% $cell[$i][$e] %></TD>
%     }
  </TR>
%   }
%
% } else { #!transpose
%
%   for (my $i = 0; $i < $num_items + 1; $i++) {
  <TR>
%     for (my $e = 0; $e < $num_entries + 1; $e++) {
    <TD CLASS="<%"cell i$i e$e"%>"><% $cell[$i][$e] %></TD>
%     }
  </TR>
%   }
</TABLE>
% }

<% include('/elements/footer.html') %>
% } 
<%once>

</%once>
<%init>

my(%opt) = @_;
my $session;
# load from cache if possible, to avoid recalculating
if ( $cgi->param('session') =~ /^(\d+)$/ ) {
  $session = $1;
  %opt = %{ $m->cache->get($session) };
}
else {
  $session = sprintf("%010d%06d", time, int(rand(1000000)));
  $m->cache->set($session, \%opt, '1h');
}

my $sprintf = $opt{'sprintf'} || '%.2f';

my $conf = new FS::Conf;
my $money_char = $opt{'disable_money'} ? '' : $conf->config('money_char');

my @items = @{ $opt{'items'} };

foreach my $other (qw( col_labels row_labels graph_labels axis_labels colors links )) {
  if ( ref($opt{$other}) eq 'HASH' ) {
    $opt{$other} = [ map $opt{$other}{$_}, @items ];
  }
}

my @col_labels = @{$opt{'col_labels'}};
my @row_labels = @{$opt{'row_labels'}};
my @data       = @{$opt{'data'}};

$opt{'axis_labels'}  ||= $opt{'col_labels'};
$opt{'graph_labels'} ||= $opt{'row_labels'};

my $filename = $cgi->url(-relative => 1);
$filename =~ s/\.(cgi|html)$//;

</%init>
