<% header(emt("Package $past_method")) %>
  <SCRIPT TYPE="text/javascript">
    window.top.location.reload();
  </SCRIPT>
  </BODY>
</HTML>
<%once>

my %past = ( 'cancel'   => 'cancelled',
             'expire'   => 'expired',
             'suspend'  => 'suspended',
             'adjourn'  => 'adjourned',
             'resume'   => 'scheduled to resume',
             'uncancel' => 'un-cancelled',
           );

#i'm sure this is false laziness with somewhere, at least w/misc/cancel_pkg.html
my %right = ( 'cancel'   => 'Cancel customer package immediately',
              'expire'   => 'Cancel customer package later',
              'suspend'  => 'Suspend customer package',
              'adjourn'  => 'Suspend customer package later',
              'resume'   => 'Unsuspend customer package', #later?
              'uncancel' => 'Un-cancel customer package',
            );

</%once>
<%init>

#untaint method
my $method = $cgi->param('method');
$method =~ /^(cancel|expire|suspend|adjourn|resume|uncancel)$/
  or die "Illegal method";
$method = $1;
my $past_method = $past{$method};

die "access denied"
  unless $FS::CurrentUser::CurrentUser->access_right($right{$method});

#untaint pkgnum
my $pkgnum = $cgi->param('pkgnum');
$pkgnum =~ /^(\d+)$/ or die "Illegal pkgnum";
$pkgnum = $1;

my $date = time;
if ($method eq 'expire' || $method eq 'adjourn' || $method eq 'resume') {
  #untaint date
  $date = $cgi->param('date'); #huh?
  parse_datetime($cgi->param('date')) =~ /^(\d+)$/ or die "Illegal date";
  $date = $1;
  $method = 'cancel'    if $method eq 'expire';
  $method = 'suspend'   if $method eq 'adjourn';
  $method = 'unsuspend' if $method eq 'resume';
}

my $resume_date;
if ( $method eq 'suspend' ) { #or 'adjourn'
  $resume_date = parse_datetime($cgi->param('resume_date'))
    if $cgi->param('resume_date');
}

my $cust_pkg = qsearchs( 'cust_pkg', {'pkgnum'=>$pkgnum} );

#untaint reasonnum
my $reasonnum = $cgi->param('reasonnum');
if ( $method !~ /^(unsuspend|uncancel)$/ ) {
  $reasonnum =~ /^(-?\d+)$/ or die "Illegal reasonnum";
  $reasonnum = $1;

  if ($reasonnum == -1) {
    $reasonnum = {
      'typenum' => scalar( $cgi->param('newreasonnumT') ),
      'reason'  => scalar( $cgi->param('newreasonnum' ) ),
    };
  }
}

#for uncancel
my $last_bill =
  $cgi->param('last_bill') ? parse_datetime($cgi->param('last_bill')) : '';
my $bill =
  $cgi->param('bill')      ? parse_datetime($cgi->param('bill'))      : '';

my $error = $cust_pkg->$method( 'reason'      => $reasonnum,
                                'date'        => $date,
                                'resume_date' => $resume_date,
                                'last_bill'   => $last_bill,
                                'bill'        => $bill,
                              );

if ($error) {
  $cgi->param('error', $error);
  print $cgi->redirect(popurl(2). "cancel_pkg.html?". $cgi->query_string );
}

</%init>
