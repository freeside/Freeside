<& /elements/header-popup.html, $quotationnum ? mt('Add package to quotation')
                                              : mt('Order new package')
&>

<LINK REL="stylesheet" TYPE="text/css" HREF="../elements/calendar-win2k-2.css" TITLE="win2k-2">
<SCRIPT TYPE="text/javascript" SRC="../elements/calendar_stripped.js"></SCRIPT>
<SCRIPT TYPE="text/javascript" SRC="../elements/calendar-en.js"></SCRIPT>
<SCRIPT TYPE="text/javascript" SRC="../elements/calendar-setup.js"></SCRIPT>

<SCRIPT TYPE="text/javascript" SRC="../elements/order_pkg.js"></SCRIPT>

<& /elements/error.html &>

<FORM NAME="OrderPkgForm" ACTION="<% $p %>edit/process/quick-cust_pkg.cgi" METHOD="POST">

<INPUT TYPE="hidden" NAME="custnum" VALUE="<% $cust_main ? $cust_main->custnum : '' %>">
<INPUT TYPE="hidden" NAME="prospectnum" VALUE="<% $prospect_main ? $prospect_main->prospectnum : '' %>">
<INPUT TYPE="hidden" NAME="qualnum" VALUE="<% scalar($cgi->param('qualnum')) |h %>">
<INPUT TYPE="hidden" NAME="quotationnum" VALUE="<% $quotationnum %>">
% if ( $svcpart ) {
    <INPUT TYPE="hidden" NAME="svcpart" VALUE="<% $svcpart %>">
% }

<% ntable("#cccccc", 2) %>
% if ( $part_pkg ) {
    <INPUT TYPE="hidden" NAME="pkgpart" VALUE="<% $part_pkg->pkgpart %>">
    <TR>
      <TH ALIGN="right"><% mt('Package') |h %></TH>
      <TD COLSPAN=6><% $part_pkg->pkg_comment |h %></TD>
    </TR>
% } else {
    <& /elements/tr-select-cust-part_pkg.html,
                 'curr_value'    => $pkgpart,
                 'classnum'      => -1,
                 'cust_main'     => $cust_main,
                 'prospect_main' => $prospect_main,
    &>
% }

% if ( $conf->exists('invoice-unitprice') ) {
    <TR>
      <TH ALIGN="right"><% mt('Quantity') |h %> </TD>
      <TD>
        <INPUT TYPE="text" NAME="quantity" SIZE=4 VALUE="<% $quantity %>">
      </TD>
    </TR>
% } else {
    <INPUT TYPE="hidden" NAME="quantity" VALUE="1">
% }

<TR>
  <TH ALIGN="right"><% mt('Start date') |h %> </TD>
  <TD COLSPAN=6>
    <& /elements/input-date-field.html,{
                'name'      => 'start_date',
                'format'    => $date_format,
                'value'     => $start_date,
                'noinit'    => 1,
              } &>
    <FONT SIZE=-1>(<% mt('leave blank to start immediately') |h %>)</FONT>
  </TD>
</TR>

% if ( $cust_main && $cust_main->payby =~ /^(CARD|CHEK)$/ ) {
%   my $what = lc(FS::payby->shortname($cust_main->payby));
    <TR>
      <TH ALIGN="right"><% mt("Disable automatic $what charge") |h %> </TH>
      <TD COLSPAN=6><INPUT TYPE="checkbox" NAME="no_auto" VALUE="Y"></TD>
    </TR>
% }

% if ( $curuser->access_right('Discount customer package') ) {
  <& /elements/tr-select-discount.html,
               'element_etc' => 'DISABLED',
               'colspan'     => 7,
               'cgi'         => $cgi,
  &>
% }

% if ( $curuser->access_right('Waive setup fee') ) {
    <TR>
      <TH ALIGN="right"><% mt('Waive setup fee') |h %> </TH>
      <TD COLSPAN=6><INPUT TYPE="checkbox" NAME="waive_setup" VALUE="Y"></TD>
    </TR>
% }

% if ( $conf->exists('pkg_referral') ) {
  <& /elements/tr-select-part_referral.html,
               'curr_value'    => scalar( $cgi->param('refnum') ), #get rid of empty_label first# || $cust_main->refnum,
               'disable_empty' => 1,
               'multiple'      => $conf->exists('pkg_referral-multiple'),
               'colspan'       => 7,
  &>
% }

% if ( $cgi->param('lock_locationnum') ) {

    <INPUT TYPE  = "hidden"
           NAME  = "locationnum"
           ID    = "locationnum"
           VALUE = "<% scalar($cgi->param('lock_locationnum')) |h %>"
    >

% } else {

    <& /elements/tr-select-cust_location.html,
                 'cgi'           => $cgi,
                 'cust_main'     => $cust_main,
                 'prospect_main' => $prospect_main,
    &>

% }

<TR>
  <TH ALIGN="right"><% mt('Contract end date') |h %> </TD>
  <TD COLSPAN=6>
    <& /elements/input-date-field.html,{
                'name'      => 'contract_end',
                'format'    => $date_format,
                'value'     => '',
                'noinit'    => 1,
                } &>
  </TD>
</TR>

</TABLE>

% unless ( $cgi->param('lock_locationnum') ) {

  <& /elements/standardize_locations.html,
                'form'       => "OrderPkgForm",
                'onlyship'   => 1,
                'no_company' => 1,
                'callback'   => 'document.OrderPkgForm.submit();',
  &>

% }

<BR>
% my $onclick = $cgi->param('lock_locationnum')
%                 ? 'document.OrderPkgForm.submit()'
%                 : 'standardize_new_location()';
<INPUT NAME    = "submitButton"
       TYPE    = "button"
       VALUE   = "<% mt("Order Package") |h %>"
       onClick = "this.disabled=true; <% $onclick %>;"
       <% $pkgpart ? '' : 'DISABLED' %>
>

</FORM>
</BODY>
</HTML>
<%init>

my $curuser = $FS::CurrentUser::CurrentUser;

die "access denied"
  unless $curuser->access_right('Order customer package');

my $conf = new FS::Conf;
my $date_format = $conf->config('date_format') || '%m/%d/%Y';

my $cust_main = '';
if ( $cgi->param('custnum') =~ /^(\d+)$/ ) {
  my $custnum = $1;
  $cust_main = qsearchs({
    'table'     => 'cust_main',
    'hashref'   => { 'custnum' => $custnum },
    'extra_sql' => ' AND '. $curuser->agentnums_sql,
  });
}

my $prospect_main = '';
if ( $cgi->param('prospectnum') =~ /^(\d+)$/ ) {
  my $prospectnum = $1;
  $prospect_main = qsearchs({
    'table'     => 'prospect_main',
    'hashref'   => { 'prospectnum' => $prospectnum },
    'extra_sql' => ' AND '. $curuser->agentnums_sql,
  });
}

my $quotationnum = '';
if ( $cgi->param('quotationnum') =~ /^(\d+)$/ ) {
  $quotationnum = $1;
}

die 'no custnum or prospectnum' unless $cust_main || $prospect_main;

my $part_pkg = '';
if ( $cgi->param('lock_pkgpart') ) {
  $part_pkg = qsearchs({
    'table'     => 'part_pkg',
    'hashref'   => { 'pkgpart' => scalar($cgi->param('lock_pkgpart')) },
    'extra_sql' => ' AND '. FS::part_pkg->agent_pkgs_sql(
                              $cust_main ? $cust_main->agent
                                         : $prospect_main->agent
                            ),
  })
    or die "unknown pkgpart ". $cgi->param('lock_pkgpart');
}

my $pkgpart = $part_pkg ? $part_pkg->pkgpart : scalar($cgi->param('pkgpart'));

my $quantity = 1;
if ( $cgi->param('quantity') =~ /^\s*(\d+)\s*$/ ) {
  $quantity = $1;
}

my $format = $date_format. ' %T %z (%Z)'; #false laziness w/REAL_cust_pkg.cgi?
my $start_date = '';
if( ! $conf->exists('order_pkg-no_start_date') && $cust_main ) {
  $start_date = $cust_main->next_bill_date;
  $start_date = $start_date ? time2str($format, $start_date) : '';
}

my $svcpart = scalar($cgi->param('svcpart'));

</%init>
