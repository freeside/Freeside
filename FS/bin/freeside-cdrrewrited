#!/usr/bin/perl -w

use strict;
use vars qw( $conf );
use FS::Daemon ':all'; #daemonize1 drop_root daemonize2 myexit logfile sig*
use FS::UID qw( adminsuidsetup );
use FS::Record qw( qsearch qsearchs );
#use FS::cdr;
#use FS::cust_pkg;
#use FS::queue;

my $user = shift or die &usage;

#daemonize1('freeside-sprepaidd', $user); #keep unique pid files w/multi installs
daemonize1('freeside-cdrrewrited');

drop_root();

adminsuidsetup($user);

logfile( "%%%FREESIDE_LOG%%%/cdrrewrited-log.". $FS::UID::datasrc );

daemonize2();

$conf = new FS::Conf;

die "not running; cdr-asterisk_forward_rewrite, cdr-charged_party_rewrite ".
    " and cdr-taqua-accountcode_rewrite conf options are all off\n"
  unless _shouldrun();

#--

my %accountcode_unmatch = ();
my $accountcode_retry = 4 * 60 * 60; # 4 hours
my $accountcode_giveup = 4 * 24 * 60 * 60; # 4 days

my %cdr_type = map { lc($_->cdrtypename) => $_->cdrtypenum } 
  qsearch('cdr_type',{});

while (1) {

  #hmm... don't want to do an expensive search with an ever-growing bunch
  # of unprocessed CDRs during the month... better to mark them all as
  # rewritten "skipped", i.e. why we're a daemon in the first place
  # instead of just doing this search like normal CDRs

  #hmm :/
  my @recent = grep { ($accountcode_unmatch{$_} + $accountcode_retry) > time }
                 keys %accountcode_unmatch;
  my $extra_sql = scalar(@recent)
                    ? ' AND acctid NOT IN ('. join(',', @recent). ') '
                    : '';

  my $found = 0;
  my %skip = ();
  my %warning = ();

  foreach my $cdr ( 
    qsearch( {
      'table'     => 'cdr',
      'extra_sql' => 'FOR UPDATE',
      'hashref'   => {},
      'extra_sql' => 'WHERE freesidestatus IS NULL '.
                     ' AND freesiderewritestatus IS NULL '.
                     $extra_sql.
                     ' LIMIT 1024', #arbitrary, but don't eat too much memory
    } )
  ) {

    next if $skip{$cdr->acctid};

    $found = 1;
    my @status = ();

    if ( $conf->exists('cdr-asterisk_forward_rewrite')
         && $cdr->dstchannel =~ /^Local\/(\d+)/i && $1 ne $cdr->dst
       )
    {

      my $dst = $1;

      warn "dst ". $cdr->dst. " does not match dstchannel $dst ".
           "(". $cdr->dstchannel. "); rewriting CDR as a forwarded call";

      $cdr->charged_party($cdr->dst);
      $cdr->dst($dst);
      $cdr->amaflags(2);

      push @status, 'asterisk_forward';

    }

    # XXX weird special case stuff--can we modularize this somehow?
    # reference RT#16271
    if ( $conf->exists('cdr-asterisk_australia_rewrite') and
         $cdr->disposition eq 'ANSWERED' ) {
      my $dst = $cdr->dst;
      my $type;
      if ( $dst =~ /^0?(12|13|1800|1900|0055)/ ) {
        # toll free or smart numbers, any length
        $type = 'tollfree';
        $cdr->charged_party($dst);
      }
      elsif ( $dst =~ /^(11|0011)/ ) {
        # will be followed by country code
        $type = 'international';
        $dst =~ s/^$1/0011/; #standardize
        $cdr->dst($dst);
      }
      elsif ( length($dst) == 10 and$dst =~ /^04/ ) {
        $type = 'mobile';
      }
      elsif ( length($dst) == 10 and $dst =~ /^02|03|07|08/ ) {
        $type = 'domestic';
      }
      elsif ( length($dst) == 8 ) {
        # local call, no area code
        $type = 'domestic';
      }
      else {
        $type = 'other';
      }
      if ( $type and exists($cdr_type{$type}) ) {
        $cdr->cdrtypenum($cdr_type{$type});
        push @status, 'asterisk_australia';
      }
      else {
        $warning{"no CDR type defined for $type calls"}++;
      }
    }

    if ( $conf->exists('cdr-charged_party_rewrite') && ! $cdr->charged_party ) {

      $cdr->set_charged_party;
      push @status, 'charged_party';

    }

    if ( $conf->exists('cdr-taqua-accountcode_rewrite')
         && $cdr->lastapp eq 'acctcode' && $cdr->cdrtypenum == 1
       )
    {

      #find the matching CDR
      my $primary = qsearchs('cdr', {
        'sessionnum'  => $cdr->sessionnum,
        'src'         => $cdr->subscriber,
        #'accountcode' => '',
      });

      unless ( $primary ) {

        my $cantfind = "can't find primary CDR with session ". $cdr->sessionnum.
                       ", src ". $cdr->subscriber;
        if ( $cdr->calldate_unix + $accountcode_giveup < time ) {
          warn "ERROR: $cantfind; giving up\n";
          push @status, 'taqua-accountcode-NOTFOUND';
          $cdr->status('done'); #so it doesn't try to rate
          delete $accountcode_unmatch{$cdr->acctid}; #so it doesn't suck mem
        } else {
          warn "WARNING: $cantfind; will keep trying\n";
          $accountcode_unmatch{$cdr->acctid} = time;
          next;
        }

      } else {

        $primary->accountcode( $cdr->lastdata );
        #$primary->freesiderewritestatus( 'taqua-accountcode-primary' );
        my $error = $primary->replace;
        if ( $error ) {
          warn "WARNING: error rewriting primary CDR (will retry): $error\n";
          next;
        }
        $skip{$primary->acctid} = 1;

        push @status, 'taqua-accountcode';
        $cdr->status('done'); #so it doesn't try to rate

      }

    }

    $cdr->freesiderewritestatus(
      scalar(@status) ? join('/', @status) : 'skipped'
    );

    my $error = $cdr->replace;

    if ( $error ) {
      warn "WARNING: error rewriting CDR (will retry in 30 seconds):".
           " $error\n";
      sleep 30; #i dunno, wait and see if the database comes back?
    }

    last if sigterm() || sigint();

  }

  foreach (sort keys %warning) {
    warn "WARNING: $_ (x $warning{$_})\n";
  }
  %warning = ();

  myexit() if sigterm() || sigint();
  #sleep 1 unless $found;
  sleep 5 unless $found;

}

#--

sub _shouldrun {
     $conf->exists('cdr-asterisk_forward_rewrite')
  || $conf->exists('cdr-asterisk_australia_rewrite')
  || $conf->exists('cdr-charged_party_rewrite')
  || $conf->exists('cdr-taqua-accountcode_rewrite');
}

sub usage { 
  die "Usage:\n\n  freeside-cdrrewrited user\n";
}

=head1 NAME

freeside-cdrrewrited - Real-time daemon for CDR rewriting

=head1 SYNOPSIS

  freeside-cdrrewrited

=head1 DESCRIPTION

Runs continuously, searches for CDRs and does forwarded-call rewriting if any
of the "cdr-asterisk_forward_rewrite", "cdr-charged_party_rewrite" or
"cdr-taqua-accountcode_rewrite" config options are enabled.

=head1 SEE ALSO

=cut

1;
